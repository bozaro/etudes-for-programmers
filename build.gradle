apply plugin: "java"

project.ext.sourceCompatibility = JavaVersion.VERSION_1_7

repositories {
	mavenCentral()	
}

subprojects {
	group = "ru.bozaro"
	version = "1.0-SNAPSHOT"

	apply plugin: "java"

	test {
		ignoreFailures = true
	}

	repositories {
		mavenCentral()	
	}

	dependencies {
		compile "com.intellij:annotations:9.0.4"
		compile "args4j:args4j:2.0.23"
		compile "org.apache.commons:commons-io:1.3.2"
		testCompile "junit:junit:4.11"
	}
}

dependencies {
	runtime files ("lib/jeuclid-fop-3.1.9.jar")
	runtime "net.sourceforge.jeuclid:jeuclid-core:3.1.9"
	runtime "net.sf.offo:fop-hyph:1.2"
	runtime "org.apache.xmlgraphics:fop:1.0"
}

task(book, dependsOn: 'classes', type: JavaExec) {
	main = "org.apache.fop.cli.Main"
	classpath = sourceSets.main.runtimeClasspath
	args = [
		"-c", "config.xml",
		"-l", "ru",
		"-xml", "book.xml",
		"-xsl", "/usr/share/sgml/docbook/stylesheet/xsl/docbook-xsl/fo/docbook.xsl",
		"-pdf", "build/book.pdf",
		"-param", "body.font.family", "Serif",
		"-param", "title.font.family", "Serif",
		"-param", "monospace.font.family", "Mono",
		"-param", "use.extensions", "1",
		"-param", "fop1.extensions", "1",
	]
}

defaultTasks 'book'
